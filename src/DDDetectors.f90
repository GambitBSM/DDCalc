MODULE DDDetectors

!XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
! DDDetectors
!    Routines for initializing and modifying the detector setup,
!    notatably by setting isotopes to use, loading efficiencies from
!    file, and tabulating form factors.
!XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

USE DDTypes
USE DDCommandLine
USE DDNuclear
USE DDUtils
USE DDInput
USE DDOutput

IMPLICIT NONE

PUBLIC :: DDCalc_GetDetector,DDCalc_SetDetector,&
          DDCalc_InitDetectorDataCommandLine
INTERFACE DDCalc_GetDetector
  MODULE PROCEDURE GetDetector
END INTERFACE
INTERFACE DDCalc_SetDetector
  MODULE PROCEDURE SetDetector
END INTERFACE
INTERFACE DDCalc_InitDetectorDataCommandLine
  MODULE PROCEDURE InitDetectorDataCommandLine
END INTERFACE

CONTAINS


!=======================================================================
! DETECTOR EFFICIENCY/RESPONSE ROUTINES
!=======================================================================

! ----------------------------------------------------------------------
! Sets the efficiencies to those found in the given file, presumably
! an efficiency or efficiencies file generated by TPCMC.
! 
! Required input argument:
!     file       The file to load efficiencies from.
! Required output arguments:
!     NE         Number of recoil energy tabulation points loaded
!     E          Allocatable array to contain energies [keV].
!                Allocated to size [1:NE].
!     Neff       Number of interval/bin efficiencies loaded.
!     eff        Allocatable array to contain efficiency curves for
!                the total range and each interval/bin.  Allocated to
!                size [1:NE,0:Neff].
! 
SUBROUTINE LoadEfficiencyFile(file,NE,E,Neff,eff)
  IMPLICIT NONE
  CHARACTER(LEN=*), INTENT(IN) :: file
  INTEGER, INTENT(OUT) :: NE,Neff
  REAL*8, ALLOCATABLE, INTENT(OUT) :: E(:),eff(:,:)
  LOGICAL :: status
  INTEGER :: Kcol,Keff,Nrow,Ncol,Nvalid
  REAL*8, ALLOCATABLE :: data(:,:)
  
  ! Load table from file
  CALL LoadTable(file=file,Nrow=Nrow,Ncol=Ncol,data=data,status=status)
  IF ((.NOT. status) .OR. (Ncol .LT. 2)) THEN
    WRITE(0,*) 'ERROR: Failed to load data from file ' // TRIM(file) // '.'
    STOP
  END IF
  
  ! Energies
  NE = Nrow
  ALLOCATE(E(NE))
  E = data(:,1)
  
  ! Find number of valid efficiency columns
  Nvalid = 0
  DO Kcol = 2,Ncol
    IF (ALL(data(:,Kcol) .GE. 0d0) .AND. ALL(data(:,Kcol) .LE. 1.00001d0)) Nvalid = Nvalid + 1
  END DO
  IF (Nvalid .LE. 0) THEN
    WRITE(0,*) 'ERROR: Failed to find valid data in file ' // TRIM(file) // '.'
    STOP
  END IF
  
  ! Now get efficiencies
  Neff = Nvalid - 1
  ALLOCATE(eff(NE,0:Neff))
  Keff = 0
  DO Kcol = 2,Ncol
    IF (ALL(data(:,Kcol) .GE. 0d0) .AND. ALL(data(:,Kcol) .LE. 1.00001d0) &
        .AND. (Keff .LE. Neff)) THEN
      eff(:,Keff) = data(:,Kcol)
      Keff = Keff + 1
    END IF
  END DO
  
END SUBROUTINE



!=======================================================================
! DETECTOR SETUP ROUTINES
!=======================================================================

! ----------------------------------------------------------------------
! Get various detector quantities.
! Note this only allows access to fixed quantities, not quantities that
! are recalculated for each WIMP (see GetRates() for that).
! 
! Optional input argument:
!   D           The DetectorStruct structure to extract detector
!               quantities from.  If not given, a default structure
!               (internally stored) will be used.
! Optional output arguments:
!   mass        Detector fiducial mass [kg]
!   time        Detector exposure time [day]
!   exposure    Detector exposure [kg day]
!   Nevents     Number of observed events
!   background  Average expected background events
!   Niso        Number of isotopes
!   Ziso        Allocatable integer array to be filled with isotope
!               atomic numbers. Allocated to size [1:Niso].
!   Aiso        Allocatable integer array to be filled with isotope
!               atomic mass numbers. Allocated to size [1:Niso].
!   fiso        Allocatable real array to be filled with isotope
!               mass fractions. Allocated to size [1:Niso].
!   Miso        Allocatable real array to be filled with isotope
!               nuclear masses [GeV]. Allocated to size [1:Niso].
!   NE          Number of tabulated recoil energies E
!   E           Allocatable real array to be filled with recoil energies
!               [keV]. Allocated to size [1:NE].
!   eff_file    File from which efficiencies were read
!   Neff        Number of subintervals for which efficiencies are
!               available (0 for only total interval)
!   eff         Allocatable dimension=2 array containing efficiencies
!               as a function of recoil energy. Allocated to size
!               [1:NE,0:Neff], where the first index is over recoil
!               energies and the second index is over the sub-interval
!               number (0 for the total interval).
!   Wsi,Wsd     Allocatable dimension=3 array containing weighted form
!               factors for spin-independent (SI) and spin-dependent
!               (SD) couplings.  Allocated to size [-1:1,1:NE,1:Niso].
!   intervals   LOGICAL indicating if rates for intervals/bins are
!               to be calculated (used for max gap).
! 
SUBROUTINE GetDetector(D,mass,time,exposure,Nevents,background,         &
                       Niso,Ziso,Aiso,fiso,Miso,NE,E,                   &
                       eff_file,Neff,eff,                    &
                       Wsi,Wsd,intervals)
  IMPLICIT NONE
  TYPE(DetectorStruct), INTENT(IN) :: D
  LOGICAL, INTENT(OUT), OPTIONAL :: intervals
  CHARACTER(LEN=*), INTENT(OUT), OPTIONAL :: eff_file
  INTEGER, INTENT(OUT), OPTIONAL :: Nevents,Niso,NE,Neff
  INTEGER, ALLOCATABLE, INTENT(OUT), OPTIONAL :: Ziso(:),Aiso(:)
  REAL*8, INTENT(OUT), OPTIONAL :: mass,time,exposure,background
  REAL*8, ALLOCATABLE, INTENT(OUT), OPTIONAL :: fiso(:),Miso(:),E(:),   &
          eff(:,:),Wsi(:,:,:),Wsd(:,:,:)
  
  ! Exposures
  IF (PRESENT(mass))     mass     = D%mass
  IF (PRESENT(time))     time     = D%time
  IF (PRESENT(exposure)) exposure = D%exposure
  
  ! Observed events and expected background events
  IF (PRESENT(Nevents))    Nevents    = D%Nevents
  IF (PRESENT(background)) background = D%MuBackground
  
  ! Isotope data
  IF (PRESENT(Niso)) Niso = D%Niso
  IF (PRESENT(Ziso)) THEN
    ALLOCATE(Ziso(D%Niso))
    Ziso = D%Ziso
  END IF
  IF (PRESENT(Aiso)) THEN
    ALLOCATE(Aiso(D%Niso))
    Aiso = D%Aiso
  END IF
  IF (PRESENT(fiso)) THEN
    ALLOCATE(fiso(D%Niso))
    fiso = D%fiso
  END IF
  IF (PRESENT(Miso)) THEN
    ALLOCATE(Miso(D%Niso))
    Miso = D%Miso
  END IF
  
  ! Recoil energies
  IF (PRESENT(NE)) NE = D%NE
  IF (PRESENT(E)) THEN
    ALLOCATE(E(D%NE))
    E = D%E
  END IF
  
  ! Efficiencies
  IF (PRESENT(eff_file)) eff_file = D%eff_file
  IF (PRESENT(Neff))     Neff     = D%Neff
  IF (PRESENT(eff)) THEN
    ALLOCATE(eff(D%NE,0:D%Neff))
    eff = D%eff
  END IF
  
  ! Weighted form factors
  IF (PRESENT(Wsi)) THEN
    ALLOCATE(Wsi(-1:1,D%NE,0:D%Niso))
    Wsi = D%Wsi
  END IF
  IF (PRESENT(Wsd)) THEN
    ALLOCATE(Wsd(-1:1,D%NE,0:D%Niso))
    Wsd = D%Wsd
  END IF
  
  ! Calculate rates for intervals/bins?
  IF (PRESENT(intervals)) intervals = D%intervals
  
END SUBROUTINE


! ----------------------------------------------------------------------
! Set various detector quantities.  Making changes will reset all
! per-WIMP calculations like rates.
! Note this only allows access to fixed quantities, or quantities that
! need be calculated only once, not quantities that are recalculated
! for each WIMP (see SetRates() for that).
! 
! Optional input/output argument:
!   D           The DetectorStruct structure containing detector
!               data to be modified.  If not given, a default structure
!               (internally stored) will be used.
! Optional input arguments:
!   mass        Detector fiducial mass [kg]
!   time        Detector exposure time [day]
!   exposure    Detector exposure [kg day]
!   Nevents     Number of observed events
!   background  Average expected background events
! Optional isotope-related input arguments.  Niso must be given for
! any of the arrays to be used, regardless if the number has changed.
! If Niso changes, then all of the A/Z/f arrays must be specified for
! changes to take effect (otherwise these inputs are ignored).
!   Niso        Number of isotopes
!   Ziso        Integer array of size [1:Niso] containing atomic
!               numbers.
!   Aiso        Integer array of size [1:Niso] containing atomic
!               mass numbers.
!   fiso        Array of size [1:Niso] containing isotope mass
!               fractions.
! Pre-populated isotopic abundances for compounds with given
! stoichiometry.  The first two of these must be given to take effect
! (otherwise these inputs are ignored).
!   Nelem       Number of elements in compound.
!   Zelem       Integer array of size [1:Nelem] containing atomic
!               numbers of compound elements.
!   stoich      Integer array of size [1:Nelem] containing compound
!               stoichiometry.  For example, CF3Cl would be {1,3,1}.
!               Default is 1 for each element.
! Optional recoil energy tabulation arguments.  Both are required for
! changes to take effect.
!   NE          Number of tabulated recoil energies E
!   E           Array of size [1:NE] containing recoil energies [keV].
!               This defines the energies used for calculating dR/dE
!               and its integral.
! Optional efficiency curve(s) arguments.  Efficiencies can be provided
! by file or by providing tabulation data.  If tabulation data is given,
! all of NE, E, Neff, and eff must be provided.
!   eff_file    File from which efficiencies shoud be read.
!   eff_filename Sets the stored file name _without_ loading any data
!               from the file.
!   Neff        Number of sub-interval/bin efficiencies in efficiency
!               data (0 if total only).
!   eff         Array of size [1:NE,0:Neff] containing efficiencies
!               as a function of recoil energy, where the first index
!               is over recoil energies and the second index is over
!               the sub-interval number (0 for the total interval).
! Optional analysis-related arguments.
!   intervals   Specify if rates for sub-intervals should be calculated;
!               otherwise, only the full interval is used for
!               calculations (default: true).  This basically specifies
!               if maximum gap or a Poisson is used in exclusion
!               constraints.
!   Emin        If given, sets all efficiencies below the given energy
!               [keV] to zero, removing all contributions from recoils
!               at lower energies.
!

SUBROUTINE SetDetector(D,mass,time,exposure,Nevents,background,         &
                       Niso,Ziso,Aiso,fiso,Nelem,Zelem,stoich,          &
                       NE,E,                                            &
                       eff_file,eff_filename,Neff,eff,       &
                       intervals,Emin)
  IMPLICIT NONE
  TYPE(DetectorStruct), INTENT(INOUT) :: D
  CHARACTER(LEN=*), INTENT(IN), OPTIONAL :: eff_file,eff_filename
  LOGICAL, INTENT(IN), OPTIONAL :: intervals
  INTEGER, INTENT(IN), OPTIONAL :: Nevents,Niso,Nelem,NE,Neff
  INTEGER, INTENT(IN), OPTIONAL :: Ziso(:),Aiso(:),Zelem(:),stoich(:)
  REAL*8, INTENT(IN), OPTIONAL :: mass,time,exposure,background,Emin
  REAL*8, INTENT(IN), OPTIONAL :: fiso(:),E(:),eff(:,0:)
  LOGICAL :: iso_change,E_change,eff_change
  INTEGER :: KE,Kiso,Neff0
  INTEGER, ALLOCATABLE :: stoich0(:)
 

  ! Some of the formally optional input arguments are actually mandatory for the code to work.
  ! Check whether they are all present, and if not set InitSuccess to false
  !
  ! E and NE have to be given.
  IF ( (.NOT. PRESENT(E)) .OR. (.NOT. PRESENT(NE)) ) THEN
    D%InitSuccess = .False.
    RETURN
  END IF
  ! NE has to be positive
  IF ( PRESENT(NE) ) THEN
    IF (.NOT. NE > 0) THEN
      D%InitSuccess = .False.
      RETURN      
    END IF
  END IF
  ! Either (eff and Neff) or eff_file have to be given in order to specify the efficiencies
  IF (.NOT. PRESENT(eff_file)) THEN
    IF ( (.NOT. PRESENT(eff)) .OR. (.NOT. PRESENT(Neff)) ) THEN
      D%InitSuccess = .False.
      RETURN
    END IF
  END IF


 
  ! Indicate if quantities changed, indicating need for
  ! array resizing and initialization
  iso_change = .FALSE.
  E_change   = .FALSE.
  eff_change = .FALSE.
 

  ! Exposures
  IF (PRESENT(mass)) THEN
    D%mass     = mass
    D%exposure = D%mass * D%time
  END IF
  IF (PRESENT(time)) THEN
    D%time     = time
    D%exposure = D%mass * D%time
  END IF
  IF (PRESENT(exposure)) THEN
    D%mass     = -1d0
    D%time     = -1d0
    D%exposure = exposure
  END IF
  
  ! Observed events and expected background events
  IF (PRESENT(Nevents))    D%Nevents      = Nevents
  IF (PRESENT(background)) D%MuBackground = background
  
  ! Isotope data
  IF (PRESENT(Niso)) THEN
    IF (Niso .EQ. D%Niso) THEN
      IF (PRESENT(Ziso)) THEN
        D%Ziso = Ziso(1:Niso)
        iso_change = .TRUE.
      END IF
      IF (PRESENT(Aiso)) THEN
        D%Aiso = Aiso(1:Niso)
        iso_change = .TRUE.
      END IF
      IF (PRESENT(fiso)) THEN
        D%fiso = fiso(1:Niso)
        iso_change = .TRUE.
      END IF
      IF (iso_change) THEN
        D%Miso = IsotopeMass(D%Ziso,D%Aiso)
      END IF
    ELSE IF (PRESENT(Ziso) .AND. PRESENT(Aiso) .AND. PRESENT(fiso)) THEN
      IF (ALLOCATED(D%Ziso)) DEALLOCATE(D%Ziso)
      IF (ALLOCATED(D%Aiso)) DEALLOCATE(D%Aiso)
      IF (ALLOCATED(D%fiso)) DEALLOCATE(D%fiso)
      IF (ALLOCATED(D%Miso)) DEALLOCATE(D%Miso)
      ALLOCATE(D%Ziso(Niso),D%Aiso(Niso),D%fiso(Niso),D%Miso(Niso))
      D%Niso = Niso
      D%Ziso = Ziso(1:Niso)
      D%Aiso = Aiso(1:Niso)
      D%fiso = fiso(1:Niso)
      D%Miso = IsotopeMass(D%Ziso,D%Aiso)
      iso_change = .TRUE.
    END IF
  END IF
  IF (PRESENT(Nelem) .AND. PRESENT(Zelem)) THEN
    ALLOCATE(stoich0(Nelem))
    IF (PRESENT(stoich)) THEN
      stoich0 = stoich
    ELSE
      stoich0 = 1
    END IF
    CALL CompoundIsotopeList(Nelem,Zelem,stoich0,                       &
                             D%Niso,D%Ziso,D%Aiso,D%fiso,D%Miso)
    iso_change = .TRUE.
  END IF
  
  ! Energies
  IF (ALLOCATED(D%E) .AND. (NE .NE. D%NE)) DEALLOCATE(D%E)
  IF (.NOT. ALLOCATED(D%E)) ALLOCATE(D%E(NE))
  D%NE = NE
  D%E = E(1:NE)
  IF (ALLOCATED(D%E_cache)) DEALLOCATE(D%E_cache)
  ALLOCATE(D%E_cache(D%NE))
  D%E_cache = D%E
  E_change = .TRUE.

 
  
  ! Set efficiencies
  ! ...from file
  IF (PRESENT(eff_file)) THEN
    IF (eff_file .NE. '') THEN
      D%eff_file = eff_file
      CALL LoadEfficiencyFile(eff_file,D%NE,D%E,D%Neff,D%eff)
      eff_change = .TRUE.
    END IF
  ! ...by arguments
  ELSE IF (PRESENT(Neff) .AND. PRESENT(eff)) THEN
    IF (ALLOCATED(D%eff))  DEALLOCATE(D%eff)
    ALLOCATE(D%eff(NE,0:Neff))
    D%Neff  = Neff
    D%eff   = eff(1:NE,0:Neff)
    eff_change = .TRUE.
  END IF
  
  ! Saved efficiency file name
  IF (PRESENT(eff_filename)) D%eff_file = eff_filename
  
  ! Include sub-intervals?
  IF (PRESENT(intervals)) THEN
    IF (intervals .NEQV. D%intervals) eff_change = .TRUE.
    D%intervals = intervals
  END IF
  
  ! Apply threshold cut.
  ! We move all E < Emin tabulation points to Emin.
  IF (PRESENT(Emin)) THEN
    ! First reset to original tabulation
    D%E = MAX(D%E_cache,Emin)
    E_change = .TRUE.
  END IF
  
  
  ! Calculate weighted form factors (SI)
  IF (ALLOCATED(D%Wsi)) DEALLOCATE(D%Wsi)
  ALLOCATE(D%Wsi(-1:1,D%NE,D%Niso))
  DO Kiso = 1,D%Niso
    CALL CalcWSI(D%Ziso(Kiso),D%Aiso(Kiso),D%NE,                   &
                 EToQ(D%E,D%Miso(Kiso)),D%Wsi(:,:,Kiso))
  END DO

  
  ! Calculate weighted form factors (SD)
  IF (ALLOCATED(D%Wsd)) DEALLOCATE(D%Wsd)
  ALLOCATE(D%Wsd(-1:1,D%NE,D%Niso))
  DO Kiso = 1,D%Niso
    CALL CalcWSD(D%Ziso(Kiso),D%Aiso(Kiso),D%NE,                   &
                 EToQ(D%E,D%Miso(Kiso)),D%Wsd(:,:,Kiso))
  END DO

  
  ! Resize halo velocity arrays if necessary
  IF ((iso_change .OR. E_change) .AND. (D%Niso .GE. 0)                 &
      .AND. (D%NE .GE. 0)) THEN
    IF (ALLOCATED(D%vmin)) DEALLOCATE(D%vmin)
    ALLOCATE(D%vmin(D%NE,D%Niso))
    IF (ALLOCATED(D%eta))  DEALLOCATE(D%eta)
    ALLOCATE(D%eta(D%NE,D%Niso))
  END IF
  
  ! Number of intervals/bins to do calculations for.
  ! Used for array sizing below.
  IF (D%intervals) THEN
    Neff0 = D%Neff
  ELSE
    Neff0 = 0
  END IF
  

  ! Resize dRdEiso if necessary
  IF (PRESENT(NE) .AND. PRESENT(Niso)) THEN
    IF (ALLOCATED(D%dRdEiso)) DEALLOCATE(D%dRdEiso)
    ALLOCATE(D%dRdEiso(NE,Niso))
  ELSE IF ((iso_change .OR. E_change) .AND. (D%Niso .GE. 0)            &
           .AND. (D%NE .GE. 0)) THEN
    IF (ALLOCATED(D%dRdEiso)) DEALLOCATE(D%dRdEiso)
    ALLOCATE(D%dRdEiso(D%NE,D%Niso))
  END IF


  ! Resize rate arrays if necessary
  IF ((E_change .OR. eff_change) .AND. (D%NE .GE. 0)                   &
      .AND. (D%Neff .GE. 0)) THEN
    IF (ALLOCATED(D%R)) DEALLOCATE(D%R)
    ALLOCATE(D%R(0:Neff0))
  END IF
  
  ! Resize event arrays if necessary
  IF (eff_change .AND. (D%Neff .GE. 0)) THEN
    IF (ALLOCATED(D%MuSignal)) DEALLOCATE(D%MuSignal)
    ALLOCATE(D%MuSignal(0:Neff0))
  END IF
  
  ! Set all calculable quantities to zero
  IF ((iso_change .OR. E_change .OR. eff_change)                        &
       .AND. (D%Niso .GE. 0) .AND. (D%NE .GE. 0)                      &
       .AND. (D%Neff .GE. 0)) THEN
    D%vmin        = 0d0
    D%eta         = 0d0
    D%dRdEiso     = 0d0
    D%R           = 0d0
    D%MuSignal    = 0d0
  END IF

  ! Everything went fine up to here, so set InitSuccess to True
  D%InitSuccess = .TRUE.
  
END SUBROUTINE


!-----------------------------------------------------------------------
! Initializes the detector setup from command-line parameters.
! 
! Possible options:
!   --mass=<value>       ! Detector fiducial mass [kg]
!   --time=<value>       ! Detector exposure time [day]
!   --exposure=<value>   ! Detector exposure [kg day]
!   --events=<N>         ! Number of observed events
!   --background=<value> ! Average expected background events
! Isotope-related options specifying the atomic number, atomic mass
! number, and mass fraction of the isotopes.  Must specify all three
! and they must have the same number of isotopes for changes to take
! effect:
!   --isotope-Z=<Z1>,<Z2>,<Z3>,...
!   --isotope-A=<A1>,<A2>,<A3>,...
!   --isotope-f=<f1>,<f2>,<f3>,...
! Use naturally occurring isotopes for the elements in the given
! compound specified by atomic numbers and the stoichiometry.  If
! stoichiometry is not given, it is assumed to be 1 for each element
! (equal abundances):
!   --element-Z=<Z1>,<Z2>,<Z3>,...
!   --stoichiometry=<stoich1>,<stoich2>,<stoich3>,...
! Shortcuts for specific materials by name (using natural abundances):
!   --argon
!   --germanium
!   --sodium-iodide
!   --silicon
!   --xenon
! Efficiency related options:
!   --file=<file>        ! File from which tabulated efficiencies should be read.
!                        ! First column is E [keV], any following consecutive
!                        ! columns containing values above 1.0 are ignored, and
!                        ! the remaining columns are taken to be efficiency curves.
!                        ! First efficiency is for full range, remaining efficiencies
!                        ! are for sub-intervals.  If not given, a default is used.
!   --no-intervals       ! Disable use of sub-intervals, even if available in file.
!   --Emin=<value>       ! If given, sets all efficiencies below the given energy
!                        ! [keV] to zero, removing all contributions from recoils
!                        ! at lower energies.  Not reversible.
! 
SUBROUTINE InitDetectorDataCommandLine(Detector)

  IMPLICIT NONE
  TYPE(DetectorStruct), INTENT(INOUT) :: Detector
  INTEGER :: Nevents,Niso,Nelem,N1,N2,N3
  INTEGER, ALLOCATABLE :: Ziso(:),Aiso(:),Zelem(:),stoich(:)
  REAL*8 :: mass,time,exposure,background,Emin
  REAL*8, ALLOCATABLE :: fiso(:)
  
  Niso = -1
    
  ! Update isotopes
  IF (GetLongArgIntegers('isotope-Z',Ziso,N1)                           &
      .AND. GetLongArgIntegers('isotope-A',Aiso,N2)                     &
      .AND. GetLongArgReals('isotope-f',fiso,N3)) THEN
    IF ((N1 .EQ. N2) .AND. (N1 .EQ. N3)) THEN
      Niso = N1
      CALL SetDetector(Detector,Niso=Niso,Ziso=Ziso,Aiso=Aiso,fiso=fiso)
    END IF
  ELSE IF (GetLongArgIntegers('element-Z',Zelem,Nelem)) THEN
    IF (GetLongArgIntegers('stoichiometry',stoich,N2)) THEN
      IF (N2 .EQ. Nelem) THEN
        CALL SetDetector(Detector,Nelem=Nelem,Zelem=Zelem,stoich=stoich)
      END IF
    ELSE
      IF (ALLOCATED(stoich)) DEALLOCATE(stoich)
      ALLOCATE(stoich(Nelem))
      stoich = 1
      CALL SetDetector(Detector,Nelem=Nelem,Zelem=Zelem,stoich=stoich)
    END IF
  ELSE IF (GetLongArg('argon')) THEN
    CALL SetDetector(Detector,Nelem=1,Zelem=(/18/))
  ELSE IF (GetLongArg('germanium')) THEN
    CALL SetDetector(Detector,Nelem=1,Zelem=(/32/))
  ELSE IF (GetLongArg('silicon')) THEN
    CALL SetDetector(Detector,Nelem=1,Zelem=(/14/))
  ELSE IF (GetLongArg('xenon')) THEN
    CALL SetDetector(Detector,Nelem=1,Zelem=(/54/))
  ELSE IF (GetLongArg('sodium-iodide')) THEN
    CALL SetDetector(Detector,Nelem=2,Zelem=(/11,53/),stoich=(/1,1/))
  END IF
  
  ! Update exposures
  IF (GetLongArgReal('mass',mass))         CALL SetDetector(Detector,mass=mass)
  IF (GetLongArgReal('time',time))         CALL SetDetector(Detector,time=time)
  IF (GetLongArgReal('exposure',exposure)) CALL SetDetector(Detector,exposure=exposure)
  
  ! Update observed events and expected background events
  IF (GetLongArgInteger('events',Nevents))     CALL SetDetector(Detector,Nevents=Nevents)
  IF (GetLongArgReal('background',background)) CALL SetDetector(Detector,background=background)
  
  ! Set an energy threshold
  IF (GetLongArgReal('Emin',Emin)) THEN
    CALL SetDetector(Detector,Emin=Emin)
  END IF
  
END SUBROUTINE

  
END MODULE
